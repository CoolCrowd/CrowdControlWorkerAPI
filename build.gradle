group 'coolcrowd'
version '0.5'

apply plugin: 'java'
apply plugin: 'nu.studer.jooq'
apply plugin: 'jacoco'
apply plugin: 'com.github.kt3k.coveralls'
apply plugin: 'application'

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath 'mysql:mysql-connector-java:5.+'
        classpath 'nu.studer:gradle-jooq-plugin:1.0.5'
        classpath "net.saliman:gradle-cobertura-plugin:2.3.+"
        classpath "org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.6.+"
    }
}

mainClassName = 'edu.kit.ipd.crowdcontrol.workerservice.Main'

sourceCompatibility = 1.8
targetCompatibility = 1.8


repositories {
    mavenCentral()
}

dependencies {
    compile 'org.jooq:jooq:3.+'
    compile 'org.jooq:jooq-meta:3.+'
    compile 'org.jooq:jool:0.9.+'
    compile 'mysql:mysql-connector-java:5.+'
    compile 'org.mybatis:mybatis:3.+'
    compile 'com.mchange:c3p0:0.9.5.2'
    compile ('com.sparkjava:spark-core:2.3') {
        exclude group: 'org.slf4j', module: 'slf4j-simple'
    }
    compile 'com.google.code.gson:gson:2.4'
    compile 'com.google.protobuf:protobuf-java:3.+'
    compile 'com.google.protobuf:protobuf-java-util:3.+'
    compile 'commons-validator:commons-validator:1.5.0'
    compile 'com.mashape.unirest:unirest-java:1.4.7'
    compile 'org.apache.commons:commons-io:1.3.+'
    compile 'ch.qos.logback:logback-core:1.+'
    compile 'ch.qos.logback:logback-classic:1.+'
    compile 'org.slf4j:slf4j-api:1.7.12'
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.+'
    testCompile 'commons-io:commons-io:2.+'
}

applicationDefaultJvmArgs = [
        "-Dworkerservice.config.file=/etc/workerservice/configuration.properties",
        "-Dlogback.configurationFile=/etc/workerservice/logging.xml"]

//tests

test {
    testLogging {
        events 'failed', 'skipped', 'passed'
    }
}

sourceSets {
    unitTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/test/java')
        }

        resources.srcDir file('src/test/resources')
    }

    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integrationTest/java')
        }
        resources.srcDir file('src/integrationTest/resources')
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

task integrationTest(type: Test) {
    jacoco {
        destinationFile = file("$buildDir/jacoco/integrationTest.exec")
        classDumpFile = file("$buildDir/classes/integrationTest")
    }

    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath

    testLogging {
        events 'failed', 'skipped', 'passed'
    }
}

tasks.withType(Test) {
    reports.html.destination = file("${reporting.baseDir}/${name}")
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    executionData = fileTree(dir: 'build/jacoco', include: '**/*.exec')

    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }

    afterEvaluate {
        classDirectories = files(classDirectories.files.collect {
            fileTree(dir: it,
                    exclude: ['edu/kit/ipd/crowdcontrol/objectservice/proto/**',
                              'edu/kit/ipd/crowdcontrol/workerservice/proto/**',
                              'edu/kit/ipd/crowdcontrol/workerservice/database/model/**'])
        })
    }
}


//database-model
jooq {
    sample(sourceSets.main) {
        jdbc {
            url = 'jdbc:mysql://localhost:8888'
            driver = 'com.mysql.jdbc.Driver'
            user = 'LeanderK'
            password = ''
            schema = 'crowdcontrol'
        }
        generator {
            name = 'org.jooq.util.DefaultGenerator'
            strategy {
                name = 'org.jooq.util.DefaultGeneratorStrategy'
            }
            database {
                name = 'org.jooq.util.mysql.MySQLDatabase'
                inputSchema = 'crowdcontrol'
            }
            generate {
                daos = true
            }
            target {
                packageName = "edu.kit.ipd.crowdcontrol.workerservice.database.model"
                directory = 'src/main/java'
            }
        }
    }
}

generateSampleJooqSchemaSource.enabled = false

//docker-related
task copyDist(type: Copy) {
    dependsOn "installDist"
    from "$buildDir/install/workerservice"
    into "./image/opt/workerservice"
}

task installDocker() {
    dependsOn "copyDist"
}